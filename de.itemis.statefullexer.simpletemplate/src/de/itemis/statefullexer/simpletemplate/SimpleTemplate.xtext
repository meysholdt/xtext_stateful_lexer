grammar de.itemis.statefullexer.simpletemplate.SimpleTemplate with org.eclipse.xtext.common.Terminals

generate simpleTemplate "http://www.itemis.de/statefullexer/simpletemplate/SimpleTemplate"

Model:
	elements+=Text? (elements+=Lexicalspace_Expression elements+=Text?)*;

Element:
	Text | Lexicalspace_Expression;

Text:
	text=ANY_TEXT;

Lexicalspace_Expression hidden(WS, ML_COMMENT, SL_COMMENT):
	"<" expression=Exp2 ">";

Exp2:
	Addition2;

Addition2 returns Exp2:
	Multiplication2 ({Add2.left=current} '+' right=Multiplication2)*;

Multiplication2 returns Exp2:
	Prim2 ({Mult2.left=current} '*' right=Prim2)*;

Prim2 returns Exp2:
	{Val2} name=ID | '(' Addition2 ')';

terminal ANY_TEXT:
	(!'<')+;
